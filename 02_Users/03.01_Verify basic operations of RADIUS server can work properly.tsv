*Setting*	*Value*	*Value*	*Value*	*Value*	*Value*	*Value*	*Value*
Resource	../Resource/CM_Resource.tsv
Suite Setup	Run Keywords	Fetch_CM_Token	Add_RADIUSServer	Add_RADIUSUser
Suite Teardown	Cleaning environments
Force Tags	wcxu

*Variable*	*Value*	*Value*	*Value*	*Value*	*Value*	*Value*	*Value*
&{request_data_RADIUS}	name=Auto_RADIUSServer1	server_uri_1=10.200.10.15	server_secret_1=fortinet
&{invalid_credential}	Authorization=Basic 390bac56054260fcd9f9afa855dd11d891942094	Content-Type=application/json
&{RADIUS_test_info}	name=Auto_RADIUSServer1	server_uri_1=10.200.10.15	server_secret_1=fortinet	port=1812	auth_type=ANY	username=test1	password=fortinet
&{invalid_port1}	name=Auto_RADIUSServer1	port=abc
&{invalid_port2}	name=Auto_RADIUSServer1	port=!@#

*Test Case*	*Action*	 *Argument*	*Argument*	*Argument*	*Argument*	*Argument*	*Argument*


001  Add a RADIUS server cluster with exist name will fail
	POST	/api/v1/system/radius-server/	${request_data_RADIUS}
	Response Code Should Be	400
	Response Should Contain String	RadiusServer with this name already exists.
	Last Elog Sholud Contain	Adding RADIUS server (Auto_RADIUSServer1) failed

002  Add a RADIUS server cluster with wrong username/password will fail
	POST	/api/v1/system/radius-server/	${request_data_RADIUS}	${invalid_credential}
	Response Code Should Be	401
	Response Should Contain String	Invalid username/password.

003  Test the RADIUS Server connection will be ok, when the RADIUS Server can be reached and the credential is correct
	POST	/api/v1/system/radius-server/test/	${RADIUS_test_info}
	Response Code Should Be	200

004  All RADIUS server clusters can be view
	GET	/api/v1/system/radius-server/
	Response Code Should Be	200

005  The specified RADIUS server cluster can be view
	GET	/api/v1/system/radius-server/${ID}/
	Response Code Should Be	200
	Response Should Contain Sub Dictionary
	...	name=Auto_RADIUSServer1	server_uri_1=10.200.10.15	server_secret_1=fortinet	auth_type=ANY

006  Verify the port number can't be set to characters
	PATCH	/api/v1/system/radius-server/${ID}/	${invalid_port1}
	Response Code Should Be	400

007  Verify the port number can't be set to symbols
	PATCH	/api/v1/system/radius-server/${ID}/	${invalid_port2}
	Response Code Should Be	400

008  The specified RADIUS server cluster can't be deleted when using by user
	DELETE	/api/v1/system/radius-server/${ID}/
	Response Code Should Be	400
	Response Should Contain String	RADIUS server (Auto_RADIUSServer1) deletion failed. This server is being used.

009  The specified RADIUS server cluster can be delete when not used by any one
	DELETE	/api/v1/system/user/${User_ID}
	Response Code Should Be	204
	DELETE	/api/v1/system/radius-server/${ID}/
	Response Code Should Be	204

*Keyword*	*Action*	*Argument*	*Argument*	*Argument*	*Argument*	*Argument*	*Argument*

Add_RADIUSServer
	Create_On_Setup	/api/v1/system/radius-server/	name=Auto_RADIUSServer1	server_uri_1=10.200.10.15	server_secret_1=fortinet

Add_RADIUSUser
	Create_User_On_Setup	/api/v1/system/user/	username=test1	password=fortinet	server=Auto_RADIUSServer1	comments=This is a normal RADIUS user	type=RADIUS	wildcard=True

Cleaning environments
	Delete_On_Teardown	/api/v1/system/user/	test1	username
	Delete_On_Teardown	/api/v1/system/radius-server/	Auto_RADIUSServer1	name
